package com.cep;

//list any import classes here.

import com.sbnz.physio.facts.*;
import com.sbnz.physio.events.*;
import java.util.List;

declare WorkoutDifficultyEvent
	@role(event)
end


rule "Dynamic to static"
    when
    	$rep: ExerciseReplacement(name == null)
        $t1: ExcerciseEvent(processed == false, painType == ExcerciseEvent.PainType.STIFFNESS, type == Excercise.Type.DYNAMIC, $name: name)
    then
        System.out.println("Dynamic to static".toUpperCase());
        modify ($rep) {
        	setType(Excercise.Type.STATIC),
        	setName($name),
        	setStopWorkout(false);
        }
        modify($t1) { setProcessed(true); }
        insert(new WorkoutDifficultyEvent());
end

rule "Static to rest"
    when
    	$rep: ExerciseReplacement(name == null)
        $t1: ExcerciseEvent(processed == false, type == Excercise.Type.STATIC, $name: name)
    then
    	System.out.println("Static to rest".toUpperCase());
    	modify ($rep) {
        	setType(Excercise.Type.REST),
        	setName("Pacijent neka odmori malo"),
        	setStopWorkout(false);
        }
        modify($t1) { setProcessed(true); }
      	insert(new WorkoutDifficultyEvent());
end


rule "Dynamic to rest"
	when 
		$rep: ExerciseReplacement(name == null)
		$t1: ExcerciseEvent(processed == false, painType == ExcerciseEvent.PainType.PAIN, type == Excercise.Type.DYNAMIC)
		List (size > 0) from collect ( ExcerciseEvent(this before $t1) over window:time(10m))
		not WorkoutDifficultyEvent()
	then
		System.out.println("Dynamic to rest".toUpperCase());
		modify ($rep) {
        	setType(Excercise.Type.REST),
        	setName("Pacijent neka prekine vezbe"),
        	setStopWorkout(true);
        }
        modify($t1) { setProcessed(true); }
end


rule "Many mild events"
	when 
		List($brPovreda: size) from collect( 
			InjuryEvent($injury: this, severity == InjuryEvent.InjurySeverity.MILD) over window:time(20m)
		)
		List($brPremora: size) from collect(
			ExhaustionEvent($exhaustion: this) over window:time(20m)
		)
		eval($brPovreda + $brPremora > 2)
	then
		System.out.println("Many mild events".toUpperCase());
		System.out.println("Br. povreda: " + $brPovreda);
		System.out.println("Br. premora: " + $brPremora);
		insert(new WorkoutDifficultyEvent());
end		


rule "Dynamic difficulties"
	when 
		$rep: ExerciseReplacement(name == null)
		$e1: ExcerciseEvent(processed == false, painType == ExcerciseEvent.PainType.PAIN, type == Excercise.Type.DYNAMIC, $name: name)
		$e2: WorkoutDifficultyEvent()
	then
		System.out.println("Dynamic difficulties".toUpperCase());
		modify ($rep) {
        	setType(Excercise.Type.STATIC),
        	setName($name),
        	setStopWorkout(false);
        }
        modify($e1) { setProcessed(true); }
end

rule "Dynamic injury - no other difficulties"
	when 
		$rep: ExerciseReplacement(name == null)
		$e1: InjuryEvent(processed == false, severity == InjuryEvent.InjurySeverity.SEVERE, type == Excercise.Type.DYNAMIC, $name: name)
		List (size <= 1) from collect (InjuryEvent())
		List (size == 0) from collect (ExcerciseEvent())
	then
		System.out.println("Dynamic injury - no other difficulties".toUpperCase());
		modify ($rep) {
        	setType(Excercise.Type.STATIC),
        	setName($name),
        	setStopWorkout(false);
        }
        modify($e1) { setProcessed(true); }
end

rule "Dynamic injury - other difficulties"
	when 
		$rep: ExerciseReplacement(name == null)
		$e1: InjuryEvent(processed == false, severity == InjuryEvent.InjurySeverity.SEVERE, type == Excercise.Type.DYNAMIC)
		List (size > 0) from collect (InjuryEvent(this before $e1))
		List (size > 0) from collect (ExcerciseEvent())
	then
		System.out.println("Dynamic injury - other difficulties".toUpperCase());
		modify ($rep) {
        	setType(Excercise.Type.REST),
        	setName("Pacijent neka hitno prekine sa vezbanjem"),
        	setStopWorkout(true);
        }
        modify($e1) { setProcessed(true); }
end
		
		

/*
rule "Many mild injuries"
	when 
		List (size > 1) from collect ( InjuryEvent(severity == InjuryEvent.InjurySeverity.MILD) over window:time(20m) )
	then
		insert(new WorkoutDifficultyEvent());
end

rule "Exhausts a lot"
	when 
		List (size > 2) from collect ( ExhaustionEvent() over window:time(20m))
	then
		insert(new WorkoutDifficultyEvent());
end

*/